join-lines() {
  local item
  while read item; do
    echo -n "${(q)item} "
  done
}

is_in_git_repo() {
  git rev-parse HEAD > /dev/null 2>&1
}

fzf-down() {
  fzf --height 50% --min-height 20 --border --bind ctrl-/:toggle-preview "$@"
}

_gf() {
  is_in_git_repo || return
  git -c color.status=always status --short |
  fzf-down -m --ansi --nth 2..,.. \
    --preview '(git diff --color=always -- {-1} | sed 1,4d; cat {-1})' |
  cut -c4- | sed 's/.* -> //'
}

fzf-gf-widget() { local result=$(_gf | join-lines); zle reset-prompt; LBUFFER+=$result }
zle -N fzf-gf-widget
bindkey '^f' fzf-gf-widget
